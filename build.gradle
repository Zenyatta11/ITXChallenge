plugins {
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'pmd'
    id 'com.github.spotbugs' version '5.0.13'
    id 'org.sonarqube' version '3.3'
    id 'checkstyle'
    id 'jacoco'
    id 'java'
    id 'org.liquibase.gradle' version '2.1.1'
    id 'org.openapi.generator' version '6.2.0'
}

group = 'com.alpharius'
version = '0.0.1'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.0'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.yaml:snakeyaml:2.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

pmd {
    toolVersion "6.37.0"
    ruleSets = ["$rootProject.projectDir/config/build/pmd_ruleset.xml"]
}

pmdTest {
    enabled = false
}

spotbugsMain {
    excludeFilter.set(file("$rootProject.projectDir/config/build/spotbugs_exclude_filter.xml")) 
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugsTest {
    enabled = false
}

checkstyle {
    toolVersion "10.4"
    configFile = file("$rootProject.projectDir/config/build/checkstyle_config.xml")
}

test {
    useJUnitPlatform() 
    reports {
        junitXml.required = true
        html.required = true
        junitXml.destination = file("$buildDir/reports/tests/")
    }
    
    jacocoTestCoverageVerification.dependsOn jacocoTestReport
    finalizedBy jacocoTestCoverageVerification
}

jar {
    enabled = false
}

jacocoTestReport {
    reports {
        xml.enabled true // need xml report for SonarQube scan
        html.required = true
    }
    dependsOn test
}

def testExcludeList = [
        "**/com/alpharius/propi/PropiApplication*", // spring code
        "**/com/alpharius/propi/config*" // spring code
]

sonarqube {
    properties {
        property "sonar.coverage.exclusions", testExcludeList
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude:  testExcludeList)
        }))
    }
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
}

apply from: "$projectDir/gradle/preCommit.gradle"
